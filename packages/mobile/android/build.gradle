// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "35.0.0"
        minSdkVersion = 24
        compileSdkVersion = 35
        targetSdkVersion = 35
        ndkVersion = "27.1.12297006"
        kotlinVersion = "2.1.20"
        kspVersion = "2.1.20"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("com.google.gms:google-services:4.4.1")
    }
}

apply plugin: "com.facebook.react.rootproject"

allprojects {
    def REACT_NATIVE_VERSION = new File(['node', '--print',"JSON.parse(require('fs').readFileSync(require.resolve('react-native/package.json'), 'utf-8')).version"].execute(null, rootDir).text.trim())

    configurations.all {
        resolutionStrategy {
            force "com.facebook.react:react-native:" + REACT_NATIVE_VERSION
        }
    }

      repositories {
        maven {
            url "https://maven.pkg.github.com/homebase-id/ffmpeg-kit"
            credentials {
                def localProperties = new Properties()
                def localPropertiesFile = rootProject.file('local.properties')
                if (localPropertiesFile.exists()) {
                    localProperties.load(localPropertiesFile.newDataInputStream())
                }
                username = localProperties.getProperty('GITHUB_USERNAME') ?: ''
                password = localProperties.getProperty('GITHUB_TOKEN') ?: ''
            }
        }
    }
    
}

subprojects {
    project.configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'androidx.browser') {
                details.useVersion '1.8.0'
            }
        }
    }
}